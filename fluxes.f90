SUBROUTINE FLUXES(NDIM,TSN,T2N,TA,QS,QA,PS,RG,RL,RA,RS,RSOIL,DELTA,HU,H,LEV,LEG,LETR,LE,RN,G)
 USE CONST
 USE SURF1
 IMPLICIT NONE
 INTEGER, INTENT(IN)                :: NDIM
 REAL, INTENT(IN),  DIMENSION(NDIM) :: TSN, TA, T2N, QA, QS, PS, RG, RL, &
&                                      RA, RS, RSOIL, DELTA, HU
 REAL, INTENT(OUT), DIMENSION(NDIM) :: H, LEV, LEG, LETR, LE, RN, G
 REAL, DIMENSION(NDIM) :: ZRHO
 ZRHO = PS / (RD * TA*(1.+ 0.608*QA))
 H    = ZRHO*CP*(TSN - TA - GRAV*ZREF/CP)/RA
 LEV  = ZRHO*LV*DELTA*VEG*(QS - QA)/RA
 LETR = ZRHO*LV*(1. - DELTA)*VEG*(QS - QA)/(RA + RS)
 LEG  = ZRHO*LV*(1. - VEG)*(HU*QS - QA)/(RSOIL + RA)
 LE   = LEV + LETR + LEG
 RN   = -EMIS*STEFAN*TSN**4 + (1.-ALPHA)*RG + EMIS*RL 
 !G  = RN - H - LE
 G  = 4.0*ASIN(1.)/(TAU*CT)*(TSN - T2N) ! soil heat flux instead of energy balance residual
END SUBROUTINE FLUXES
